&ACCESS RVP
&REL 106
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM EDITMASK = *
DEF RSI_Example( )

; Declaration of KRL Variables
DECL RSIERR RET	; Return Value of RSI Commands
DECL INT HAI	; ObjectID for ST_ANAIN
DECL INT HP1	; ObjectID for ST_P
DECL INT HSUM	; ObjectID for ST_SUM
DECL INT HP2	; ObjectID for ST_P
DECL INT HI		; ObjectID for ST_I
DECL INT HLIM	; ObjectID for ST_LIMIT
DECL INT HPC	; ObjectID for ST_PATHCORR
DECL INT HMON	; ObjectID for ST_MONITOR
DECL INT HGR	; ObjectID for ST_GREATER
DECL INT HLE	; ObjectID for ST_LESS
DECL INT HOR	; ObjectID for ST_OR
DECL INT HM2DO	; ObjectID for ST_MAP2DIGOUT
DECL INT HAP ; ObjectID for ST_ACTPOS
DECL CHAR IP[16]	; IP- Address for RSIMonitor

; Application Parameter
DECL REAL GAIN
DECL REAL SETVALUE
DECL REAL UPPERLIMIT
DECL REAL LOWERLIMIT

;FOLD INI
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)

;Init Applicatin Parameter
GAIN = 15
SETVALUE = 50
UPPERLIMIT = 20
LOWERLIMIT = -20

;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD

; Create RSI Context
RET=ST_ANAIN(HAI,0,1,RSIUNIT_V)
RET=ST_P(HP1,0,HAI,1,1,RSIUNIT_N)
RET=ST_SUM(HSUM,0,HP1,1,0,0,-SETVALUE)
RET=ST_P(HP2,0,HSUM,1,GAIN,'HF01') ; Unit m/s
RET=ST_I(HI,0,HP2,1,0.012,1)
RET=ST_LIMIT(HLIM,0,HI,1,LOWERLIMIT,UPPERLIMIT)
RET=ST_PATHCORR(HPC,0)
RET=ST_NEWLINK(HLIM,1,HPC,1)
RET=ST_GREATER(HGR,0,HI,1,0,0,UPPERLIMIT,0)
RET=ST_LESS(HLE,0,HI,1,0,0,LOWERLIMIT,0)
RET=ST_OR(HOR,0,HGR,1,HLE,1)
RET=ST_MAP2DIGOUT(HM2DO,0,HOR,1,1,0)
RET=ST_ACTPOS(HAP,0)
IP[]="192.0.1.2"
RET=ST_MONITOR(HMON,0,IP[],6000,1)
RET=ST_SETPARAM(HMON,1,1)
RET=ST_NEWLINK(HOR,1,HMON,2)  ; Chart 1 red:   Signal - Correction exceeded limitation
RET=ST_NEWLINK(HLIM,1,HMON,3) ; Chart 1 green: Correction
RET=ST_NEWLINK(HSUM,1,HMON,4) ; Chart 1 blue:  Offset
RET=ST_NEWLINK(HAP,1,HMON,5)  ; Chart 2 red:   Position in X
RET=ST_NEWLINK(HAP,2,HMON,6)  ; Chart 2 green: Position in Y
RET=ST_NEWLINK(HAP,3,HMON,7)  ; Chart 2 blue:  Position in Z

PTP {A1 0, A2 -90, A3 90, A4 0, A5 90, A6 0}

;Move Base in actual position
$BASE.X=$POS_ACT.X
$BASE.Y=$POS_ACT.Y
$BASE.Z=$POS_ACT.z

; Start RSI execution
RET=ST_ON()
LIN_REL {Y 100}
RET=ST_OFF()

;FOLD PTP HOME  Vel= 100 % DEFAULT;%{PE}%MKUKATPBASIS,%CMOVE,%VPTP,%P 1:PTP, 2:HOME, 3:, 5:100, 7:DEFAULT
$BWDSTART = FALSE
PDAT_ACT=PDEFAULT
FDAT_ACT=FHOME
BAS (#PTP_PARAMS,100 )
$H_POS=XHOME
PTP  XHOME
;ENDFOLD

END